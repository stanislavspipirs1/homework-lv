plugins {
    id 'org.springframework.boot' version '1.5.10.RELEASE'
    id 'org.sonarqube' version '2.6'
    id 'org.unbroken-dome.test-sets' version '1.1.0'
}
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

testSets {
    integrationTest { dirName = 'integration_test' }
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble
    doLast {
        javaexec {
            main = 'cucumber.api.cli.Main'
            classpath = configurations.cucumberRuntime + sourceSets.main.output
            args = ['--plugin', 'pretty', '--glue', 'src/integration_test/groovy', 'src/integration_test/resources']
        }
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'com.h2database:h2'
    compile 'org.json:json'

    compileOnly 'org.projectlombok:lombok:1.16.18'

    testCompile 'org.codehaus.groovy:groovy-all:2.4.10'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:1.0.2'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testCompile 'info.cukes:cucumber-junit:1.2.4'
    testCompile 'info.cukes:cucumber-groovy:1.2.4'
    testCompile 'info.cukes:cucumber-spring:1.2.4'
}